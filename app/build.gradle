buildscript {
    ext {
        anko_version = '0.10.3'
        ktx_version = '0.1'
        support_version = '27.0.2'
        roomVersion = '1.0.0'
        lifecycleVersion = '1.1.0'
    }

    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.4'
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.6.0'
        classpath "io.realm:realm-gradle-plugin:4.3.3"
        classpath 'com.google.gms:google-services:3.1.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "com.ily.pakertymer"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            android.applicationVariants.all { variant ->
                renameAPK(variant)
            }
        }
        debug {
            android.applicationVariants.all { variant ->
                renameAPK(variant)
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def renameAPK(variant) {
    variant.outputs.all { output ->
        def formattedDate = new Date().format('dd-MM-yyyy')
        outputFileName = formattedDate + "-aptimer-" + "$variant.name (v$variant.versionName vc$variant.versionCode).apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "androidx.core:core-ktx:$ktx_version"
    //Support
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    //Room
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"
    //ViewModel & LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$lifecycleVersion"
    //Bottombar
    implementation 'com.roughike:bottom-bar:2.3.1'
    //Eventbus
    implementation 'org.greenrobot:eventbus:3.0.0'
    //Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
}